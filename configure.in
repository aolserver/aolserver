#
# The contents of this file are subject to the AOLserver Public License
# Version 1.1 (the "License"); you may not use this file except in
# compliance with the License. You may obtain a copy of the License at
# http://aolserver.com/.
#
# Software distributed under the License is distributed on an "AS IS"
# basis, WITHOUT WARRANTY OF ANY KIND, either express or implied. See
# the License for the specific language governing rights and limitations
# under the License.
#
# The Original Code is AOLserver Code and related documentation
# distributed by AOL.
# 
# The Initial Developer of the Original Code is America Online,
# Inc. Portions created by AOL are Copyright (C) 1999 America Online,
# Inc. All Rights Reserved.
#
# Alternatively, the contents of this file may be used under the terms
# of the GNU General Public License (the "GPL"), in which case the
# provisions of GPL are applicable instead of those above.  If you wish
# to allow use of your version of this file only under the terms of the
# GPL and not to allow others to use your version of this file under the
# License, indicate your decision by deleting the provisions above and
# replace them with the notice and other provisions required by the GPL.
# If you do not delete the provisions above, a recipient may use your
# version of this file under either the License or the GPL.
# 
#
# $Header: /Users/dossy/Desktop/cvs/aolserver/configure.in,v 1.7 2002/09/19 22:27:55 jgdavidson Exp $
#

#
# configure.in --
#
#    AOLserver configure script.
#


AC_INIT(include/ns.h)
AC_PREREQ(2.13)
AC_PREFIX_DEFAULT(/usr/local/aolserver)

#
# Include and verify Tcl config located by --with-tcl=
#

SC_PATH_TCLCONFIG
SC_LOAD_TCLCONFIG
if test "${TCL_THREADS}" != "1"
then
    AC_MSG_ERROR([must build tcl with --enable-threads])
fi
if test "${TCL_SHARED_BUILD}" != "1"
then
    AC_MSG_ERROR([must build tcl with --enable-sahred])
fi
AC_SUBST(TCL_LIB_SPEC)
AC_SUBST(TCL_LIBS)
AC_SUBST(TCL_CC)
AC_SUBST(TCL_INCLUDE_SPEC)
AC_SUBST(TCL_RANLIB)
AC_SUBST(TCL_SHLIB_SUFFIX)
AC_SUBST(TCL_EXEC_PREFIX)
AC_SUBST(TCL_EXTRA_CFLAGS)
AC_SUBST(TCL_CFLAGS_WARNING)
AC_SUBST(TCL_SHLIB_CFLAGS)
AC_SUBST(TCL_CC_SEARCH_FLAGS)
AC_SUBST(TCL_LD_SEARCH_FLAGS)
AC_SUBST(TCL_DEFS)

#
# TCL_SHLIB_LD from tclConfig.sh is not quite right for aolserver.
#
test "x$prefix" = "xNONE" && prefix="/usr/local/aolserver"
system=`uname`
case "$system" in
    Darwin)
	LDLIB="cc -dynamiclib -install_name \$(INSTLIB)/\$(LIBFILE)"
	LDSO="cc -bundle"
	CCRPATH=
	LDRPATH=
	;;
    *)
	LDLIB="$TCL_SHLIB_LD"
	case "$LDLIB" in
	    *gcc*)
		LDLIB="$LDLIB -nostartfiles \$(CCRFLAG)"
		;;
	    *cc*)
		LDLIB="$LDLIB \$(CCRFLAG)"
		;;
	    *)
		LDLIB="$LDLIB \$(LDRFLAG)"
		;;
	esac
	CCRPATH="\$(TCL_CC_SEARCH_FLAGS)\$(INSTLIB)"
	LDRPATH="\$(TCL_LD_SEARCH_FLAGS)\$(INSTLIB)"
	if test "$TCL_EXEC_PREFIX" != "$prefix"
	then
		CCRPATH="$CCRPATH \$(TCL_CC_SEARCH_FLAGS)\$(TCL_EXEC_PREFIX)/lib"
		LDRPATH="$LDRPATH \$(TCL_LD_SEARCH_FLAGS)\$(TCL_EXEC_PREFIX)/lib"
	fi
	if test -d /usr/lib/lwp
	then
		CCRPATH="$CCRPATH \$(TCL_CC_SEARCH_FLAGS)/usr/lib/lwp"
		LDRPATH="$LDRPATH \$(TCL_LD_SEARCH_FLAGS)/usr/lib/lwp"
	fi
	LDSO="\$(LDLIB)"
	;;
esac
AC_SUBST(LDLIB)
AC_SUBST(LDSO)
AC_SUBST(CCRPATH)
AC_SUBST(LDRPATH)

#
# Additional AOLserver checks.
#

AC_CHECK_FUNCS(timegm fork1 poll drand48 random _NSGetEnviron)
AC_CHECK_HEADER(mach-o/dyld.h,[AC_DEFINE(USE_DYLD)],)
AC_CHECK_HEADER(dl.h,[AC_DEFINE(USE_DLSHL)],)
AC_MSG_CHECKING([need for dup high])
AC_TRY_RUN([
#include <stdio.h>
int
main(int argc, char **argv)
{
    return (sizeof(stdout->_file) == 1) ? 0 : 1;
}], as_ok=yes, as_ok=no, as_ok=no)
AC_MSG_RESULT($as_ok)
if test "${as_ok}" = "yes"
then
    AC_DEFINE(USE_DUPHIGH)
fi

AC_MSG_CHECKING([msg_controllen in struct msghdr])
AC_TRY_COMPILE([
#include <sys/types.h>
#include <sys/socket.h>],[
struct msghdr msg;
int *i = msg.msg_controllen;
],as_ok=yes, as_ok=no)
AC_MSG_RESULT($as_ok)
if test "$as_ok" = "yes"
then
	AC_DEFINE(HAVE_CMMSG)
fi

if test "$system" = "Linux"
then
    AC_DEFINE(USE_PROCTHREAD)
fi

#
# Create Makefile.global.
#

AC_OUTPUT(include/Makefile.global)
